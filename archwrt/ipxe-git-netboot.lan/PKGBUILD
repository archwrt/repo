# Maintainer: Edward Pacman <edward@edward-p.xyz>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=ipxe-git-netboot.lan
pkgver=1.21.1.r484.gff0f8604
pkgrel=1
pkgdesc='iPXE open source boot firmware - http://netboot.lan/menu.ipxe'
arch=('any')
url='http://www.ipxe.org/'
license=('GPL')
makedepends=('git')
install=ipxe.install
source=('git+https://github.com/ipxe/ipxe.git'
        'chain-netboot.lan.ipxe'
        'colour.h'
        'console.h'
        'crypto.h'
        'general.h'
        'general.h.efi'
      )
sha256sums=('SKIP'
            'baa09ac12fb4168b5b4baef4c63d1c69f95fefe3493d204312626873f794d367'
            '20b745aece9cbe80f0ada457fed103bcca8f8c797113f22ec2a01b23929195f7'
            '841ab16a618e9606769be3ad4df160ac35124f89372b6293f6577b79cd9157a8'
            '1c7ae7821bdfffd6b5a31e5c96e441f0b21add2172de441a77d9eba49ef6dcfa'
            '115094a1d6426707316f3eb5e0ec67c21616e281dfa7d2056886452824db524e'
            'cdd0e34fc2929443a54efa06b16d72b981601cc63ce297ad48d6a1901dff9bc6')

pkgver() {
	cd ipxe/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		printf '%s.r%s.g%s' \
			"$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG})" \
			"$(git rev-list --count ${GITTAG}..)" \
			"$(git rev-parse --short HEAD)"
	else
		printf '0.r%s.g%s' \
			"$(git rev-list --count master)" \
			"$(git rev-parse --short HEAD)"
	fi
}

prepare() {
	cd ipxe/src/
  cp ${srcdir}/{colour.h,console.h,crypto.h} config/local/
}

build() {
	cd ipxe/src/

	#BIOS
  cp ${srcdir}/general.h config/local/
  make NO_WERROR=1 \
    EMBED=${srcdir}/chain-netboot.lan.ipxe \
    bin/ipxe.pxe

	# EFI
  rm config/local/general.h
  cp ${srcdir}/general.h.efi config/local/
	make NO_WERROR=1 \
    EMBED=${srcdir}/chain-netboot.lan.ipxe \
    bin-x86_64-efi/ipxe.efi
}

package() {
	cd ipxe/

	install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/ipxe/COPYING
	install -D -m0644 COPYING.GPLv2 ${pkgdir}/usr/share/licenses/ipxe/COPYING.GPLv2
	install -D -m0644 COPYING.UBDL ${pkgdir}/usr/share/licenses/ipxe/COPYING.UBDL

	cd src/

	# iPXE targets with full driver support
	install -D -m0644 bin/ipxe.pxe ${pkgdir}/usr/lib/ipxe-netboot.lan/ipxe-netboot.lan.pxe

	# iPXE EFI targets
	install -D -m0644 bin-x86_64-efi/ipxe.efi ${pkgdir}/usr/lib/ipxe-netboot.lan/ipxe-netboot.lan-x86_64.efi
}

