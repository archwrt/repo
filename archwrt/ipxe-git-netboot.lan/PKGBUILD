# Maintainer: Edward Pacman <edward@edward-p.xyz>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=ipxe-git-netboot.lan
pkgver=1.21.1.r181.ge09e1142
pkgrel=1
pkgdesc='iPXE open source boot firmware - http://netboot.lan/menu.ipxe'
arch=('any')
url='http://www.ipxe.org/'
license=('GPL')
makedepends=('git')
install=ipxe.install
source=('git://github.com/ipxe/ipxe.git'
	'chain-netboot.lan.ipxe'
    )
sha256sums=('SKIP'
            'baa09ac12fb4168b5b4baef4c63d1c69f95fefe3493d204312626873f794d367')

pkgver() {
	cd ipxe/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		printf '%s.r%s.g%s' \
			"$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG})" \
			"$(git rev-list --count ${GITTAG}..)" \
			"$(git rev-parse --short HEAD)"
	else
		printf '0.r%s.g%s' \
			"$(git rev-list --count master)" \
			"$(git rev-parse --short HEAD)"
	fi
}

prepare() {
	cd ipxe/src/

	# change menu colors
	sed -i "/COLOR_[A-Z]*_BG/s/COLOR_BLUE/COLOR_BLACK/" config/colour.h

	# enable HTTPS
    sed -i "/DOWNLOAD_PROTO_HTTPS/c #define DOWNLOAD_PROTO_HTTPS" config/general.h 
    
    #enable IMAGE trust command
    sed -i "/IMAGE_TRUST_CMD/c #define IMAGE_TRUST_CMD" config/general.h

    # enable digest command
    sed -i "/DIGEST_CMD/c #define DIGEST_CMD" config/general.h

    # enable neighbour command
	sed -i "/NEIGHBOUR_CMD/c #define NEIGHBOUR_CMD" config/general.h

	# enable ping command
	sed -i "/PING_CMD/c #define PING_CMD" config/general.h

	# enable reboot and poweroff
	sed -i -e "/REBOOT_CMD/c #define REBOOT_CMD" \
		-e "/POWEROFF_CMD/c #define POWEROFF_CMD" config/general.h

	# disable WEP, WPA and WPA2
	sed -i -e "/CRYPTO_80211_/s/^#define/#undef/" \
		-e "/IWMGMT_CMD/c #undef IWMGMT_CMD" config/general.h
}

build() {
	cd ipxe/src/

	#BIOS
    make bin/ipxe.pxe EMBED=${srcdir}/chain-netboot.lan.ipxe

	# EFI
	make bin-i386-efi/ipxe.efi bin-x86_64-efi/ipxe.efi EMBED=${srcdir}/chain-netboot.lan.ipxe
}

package() {
	cd ipxe/

	install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/ipxe/COPYING
	install -D -m0644 COPYING.GPLv2 ${pkgdir}/usr/share/licenses/ipxe/COPYING.GPLv2
	install -D -m0644 COPYING.UBDL ${pkgdir}/usr/share/licenses/ipxe/COPYING.UBDL

	cd src/

	# iPXE targets with full driver support
	install -D -m0644 bin/ipxe.pxe ${pkgdir}/usr/lib/ipxe-netboot.lan/ipxe-netboot.lan.pxe

	# iPXE EFI targets
	install -D -m0644 bin-i386-efi/ipxe.efi ${pkgdir}/usr/lib/ipxe-netboot.lan/ipxe-netboot.lan-i386.efi
	install -D -m0644 bin-x86_64-efi/ipxe.efi ${pkgdir}/usr/lib/ipxe-netboot.lan/ipxe-netboot.lan-x86_64.efi
}

